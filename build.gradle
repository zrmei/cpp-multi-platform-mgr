buildscript {
    repositories {
        jcenter()
        maven {url 'file:gradle/repositories/gradle-cmake-plugin/'}
    }
    dependencies {
        classpath "net.freudasoft:gradle-cmake-plugin:19.199.3"
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

boolean isWindows() {
    return org.gradle.internal.os.OperatingSystem.current().isWindows()
}

Properties getCombinedProperties(List<String> names)
{
    Properties localProperties = new Properties()
    Properties p = new Properties()

    def systemDir = (isWindows() ? "C:/" : "/opt/") + rootProject.name + "-configure";
    if (project.hasProperty("propertiesDir")) {
        systemDir = project.propertiesDir
    }

    for (String s : names) {
        def cfg_file = new File(project.rootDir, s + ".properties");

        if(!cfg_file.exists()) {
            cfg_file = new File(systemDir, s + ".properties");

            if(!cfg_file.exists()) {
                continue
            }
        }
       
        p.load(new FileInputStream(cfg_file))

        for(String key : p.stringPropertyNames()) {
            localProperties.setProperty(key, p.getProperty(key, ""))
        }
    }

    return localProperties
}