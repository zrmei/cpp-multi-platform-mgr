import java.util.regex.*;

apply plugin: "net.freudasoft.gradle-cmake-plugin"

cmake {
    sourceFolder = projectDir
    buildClean = true
    jobCount = "auto"
}

String defineVariantTask(String name, String platform_, String prop_path) {

    Properties localProperties = getCombinedProperties(["local", "linux", prop_path])

    tasks.register("configure-${name}-${platform_}", net.freudasoft.CMakeConfigureTask) {
        group = "$name"

        configureFromProject()

        if (localProperties.containsKey("toolchain.path")) {
            options.CMAKE_TOOLCHAIN_FILE = localProperties.getProperty("toolchain.path", "")
        } else if (localProperties.containsKey("gcc.dir")) {
            def gccDir = localProperties.getProperty("gcc.dir", "")
            def gccPrefix = localProperties.getProperty("gcc.prefix", "")
            def gccSuffix = localProperties.getProperty("gcc.suffix", "")

            if (!gccDir.isEmpty() && !gccDir.endsWith("/")) gccDir = "${gccDir}/";

            options.CMAKE_C_COMPILER = "${gccDir}${gccPrefix}gcc${gccSuffix}"
            options.CMAKE_CXX_COMPILER = "${gccDir}${gccPrefix}g++${gccSuffix}"
        }

        options.PLATFORM = localProperties.getProperty("toolchain.platform", "")

        def cmakeDir = localProperties.getProperty("cmake.dir", "${rootProject.projectDir}/tools/cmake")
        executable = "${cmakeDir}/bin/cmake"

        options.CMAKE_SYSTEM_NAME = "Linux"
        options.CMAKE_BUILD_TYPE = "MinSizeRel"

        if (localProperties.containsKey("wsl.distribution")) {
            distribution = localProperties.getProperty("wsl.distribution", "Ubuntu-16.04")
        }

        for (Enumeration en = localProperties.propertyNames(); en.hasMoreElements();) {
            String key = en.nextElement();
            if(key.startsWith("def.")) {
                options[key.substring(4)] = localProperties.getProperty(key);
            }
        }

        if (options.getAt("PLATFORM").getOrElse("") == "") {
            options.PLATFORM = platform_
        }

        if (platform_ != "x64" && platform_ != "x86") {
            options.FRIEND_NAME = platform_
        }

        workingFolder = file("$buildDir/cmake-${name}-${platform_}")
    }

    tasks.register("build-${name}-${platform_}", net.freudasoft.CMakeBuildTask) {
        group = "$name"

        configureFromProject()

        buildConfig = localProperties.getProperty("def.CMAKE_BUILD_TYPE", "MinSizeRel");

        if (localProperties.containsKey("wsl.distribution")) {
            distribution = localProperties.getProperty("wsl.distribution", "Ubuntu-16.04")
        }

        workingFolder = file("$buildDir/cmake-${name}-${platform_}")
    }

    tasks.getByName("build-${name}-${platform_}").dependsOn("configure-${name}-${platform_}")

    if (localProperties.containsKey("dependsOn")) {
        String  dependsOn_ = localProperties.getProperty("dependsOn", "")

        def dependsOnList = []
        for(String item: dependsOn_.split(",")) {
            def task_name = item.trim();
            dependsOnList.add("build-${name}-${task_name}")
        }

         tasks.getByName("build-${name}-${platform_}").dependsOn(dependsOnList)
    }

    return "build-${name}-${platform_}"
}

fileTree(dir: "${project.projectDir}/properties", include: '**/*.properties').each {File file ->
    defineVariantTask("linux", basename(file.getName()), file.getAbsolutePath())
}

def build_targets = [
    defineVariantTask("linux", "x86", "x86"),
    defineVariantTask("linux", "x64", "x64")
]

tasks.register("build-linux-all") {
    group = "linux"
    description = "build all task defined in this group."

    dependsOn build_targets
}

if (!isWindows()) {
    exec {
        executable  "chmod"
        args "+x", "${rootProject.projectDir}/tools/ninja/ninja"
        ignoreExitValue true
    }

    exec {
        executable  "chmod"
        args "+x", "${rootProject.projectDir}/tools/cmake/bin/cmake"
        ignoreExitValue true
    }
}

tasks.register("pack-linux-release") {
    group = "linux"

    doLast {
        def rootDir = rootProject.projectDir
        def version = ""

        copy {
            def distDir = new File("${projectDir}/Linux_aiui_soft/include/aiui");
            distDir.mkdirs()

            from "${rootDir}/src/include/aiui"
            into distDir
        }

        copy {
            def sourceDir = new File("${rootDir}/output/Linux/x64_GNU_5.4.0/MinSizeRel/libaiui.so")
            def distDir = new File("${projectDir}/Linux_aiui_soft/libs/x64")
            distDir.mkdirs()

            from sourceDir
            into distDir

            def pattern = "(5\\.6\\.\\d+\\.\\d+)"
            def r = Pattern.compile(pattern)
            def m = r.matcher(sourceDir.text)

            version = m.find() ? m.group(0) : ""
        }

        copy {
            def distDir = new File("${projectDir}/Linux_aiui_soft/libs/x86");
            distDir.mkdirs()

            from "${rootDir}/output/Linux/x86_GNU_5.4.0/MinSizeRel/libaiui.so"
            into distDir
        }

        exec {
            workingDir "Linux_aiui_soft/samples/"
            executable  isWindows() ? "${rootDir}/tools/dos2unix-7.4.2-win64/dos2unix.exe": "dos2unix"
            args "64bit_make.sh", "32bit_make.sh"
            ignoreExitValue true
        }

        zipFile("${projectDir}/Linux_aiui_soft", "${projectDir}/Linux_aiui_soft_${version}.zip")
    }
}


tasks.register("build-and-pack-linux-release") {
    group = "linux"

    dependsOn "build-linux-all", "pack-linux-release"
}